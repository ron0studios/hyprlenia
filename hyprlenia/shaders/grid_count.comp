#version 460 core

 

layout(local_size_x = 256) in;

layout(std430, binding = 0) readonly buffer ParticlesIn {
  float particles[];  
                      
};

layout(std430, binding = 1) buffer GridCounters {
  uint counters[];  
};

uniform int u_NumParticles;
uniform float u_WorldWidth;
uniform float u_WorldHeight;
uniform float u_CellSize;
uniform int u_GridWidth;
uniform int u_GridHeight;


int positionToCell(vec2 pos) {
  
  float x = pos.x + u_WorldWidth;
  float y = pos.y + u_WorldHeight;

  
  int cellX = clamp(int(x / u_CellSize), 0, u_GridWidth - 1);
  int cellY = clamp(int(y / u_CellSize), 0, u_GridHeight - 1);

  return cellY * u_GridWidth + cellX;
}

void main() {
  uint idx = gl_GlobalInvocationID.x;
  if (idx >= u_NumParticles) return;

  int base = int(idx) * 12;
  vec2 pos = vec2(particles[base], particles[base + 1]);
  float energy = particles[base + 4];

  
  int cellId;
  if (energy < 0.01) {
    cellId = 0;  
  } else {
    cellId = positionToCell(pos);
  }

  
  atomicAdd(counters[cellId], 1u);
}

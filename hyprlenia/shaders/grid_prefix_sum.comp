#version 460 core

 

layout(local_size_x = 512) in;

layout(std430, binding = 0) buffer GridCounters {
  uint counters[];  
};

layout(std430, binding = 1) buffer GridOffsets {
  uint offsets[];  
};

shared uint temp[1024];  

uniform int u_NumCells;

void main() {
  uint tid = gl_LocalInvocationID.x;
  uint n = uint(u_NumCells);

  
  uint offset = 1u;

  
  uint ai = tid;
  uint bi = tid + 512u;

  if (ai < n)
    temp[ai] = counters[ai];
  else
    temp[ai] = 0u;

  if (bi < n)
    temp[bi] = counters[bi];
  else
    temp[bi] = 0u;

  
  for (uint d = 512u; d > 0u; d >>= 1u) {
    barrier();
    if (tid < d) {
      uint ai2 = offset * (2u * tid + 1u) - 1u;
      uint bi2 = offset * (2u * tid + 2u) - 1u;
      if (bi2 < 1024u) {
        temp[bi2] += temp[ai2];
      }
    }
    offset *= 2u;
  }

  
  barrier();
  if (tid == 0u) {
    temp[1023] = 0u;
  }

  
  for (uint d = 1u; d < 1024u; d *= 2u) {
    offset >>= 1u;
    barrier();
    if (tid < d) {
      uint ai2 = offset * (2u * tid + 1u) - 1u;
      uint bi2 = offset * (2u * tid + 2u) - 1u;
      if (bi2 < 1024u) {
        uint t = temp[ai2];
        temp[ai2] = temp[bi2];
        temp[bi2] += t;
      }
    }
  }

  barrier();

  
  if (ai < n) {
    counters[ai] = temp[ai];
    offsets[ai] = temp[ai];  
  }
  if (bi < n) {
    counters[bi] = temp[bi];
    offsets[bi] = temp[bi];
  }
}
